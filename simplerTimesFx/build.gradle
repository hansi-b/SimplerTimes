plugins {
	id 'application'
	id 'distribution'

	id 'org.hansib.java-common'

	id 'org.openjfx.javafxplugin' version '0.0.12'
	id 'org.beryx.runtime' version '1.12.7'
}

javafx {
	version = '17'
	modules = [
		'javafx.controls',
		'javafx.fxml',
		'javafx.graphics'
	]
}

dependencies {

	implementation project(':simplerTimesCore')

	implementation 'net.harawata:appdirs:1.2.1'

	implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.2'

	implementation 'org.kordamp.ikonli:ikonli-javafx:12.2.0'
	implementation 'org.kordamp.ikonli:ikonli-codicons-pack:12.2.0'

	testImplementation 'org.hamcrest:hamcrest-core:2.2'
	testImplementation 'org.testfx:testfx-core:4.0.16-alpha'
	testImplementation 'org.testfx:testfx-spock:4.0.16-alpha'

	testRuntimeOnly 'org.testfx:openjfx-monocle:jdk-12.0.1+2'
}

application {
	mainModule = 'org.hansib.simplerTimes'
	mainClass = 'org.hansib.simplerTimes.AppStarter'
}


def versionedProjectName = "$project.name-${project.version}"

def currentOs = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem.name.toLowerCase()
def isWin = currentOs.contains('win')
assert isWin || currentOs.contains('linux'): "Cannot handle OS \"$currentOs\""


runtime {

	modules = ['jdk.localedata']
	additive = true // add to suggested modules

	options = [
		'--strip-debug',
		'--compress',
		'2',
		'--no-header-files',
		'--no-man-pages'
	]

	launcher {
		jvmArgs = [
			'-Dlog4j.configurationFile=log4j2_prod.yaml',
		]
		noConsole = true
	}

	jpackage {

		imageName = "${versionedProjectName}"

		appVersion = "${project.version}".split('-')[0]
		installerType = isWin ? "msi" : "deb"

		def iconExt = isWin ? 'ico' : 'png'
		imageOptions += [
			"--icon",
			"src/main/resources/logo.$iconExt"
		]

		installerOptions += [
			"--resource-dir",
			"src/main/resources",
			"--file-associations",
			"src/main/resources/file_assoc_aba.properties"
		]
		if (isWin) {
			installerOptions += [
				"--win-menu",
				"--win-shortcut",
				"--win-per-user-install"
			]
		}
	}
}

