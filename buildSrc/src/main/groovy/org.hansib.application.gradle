plugins {
	id 'application'
	id 'org.hansib.java-common'

	id 'org.beryx.runtime'
	id 'com.github.hierynomus.license'
}

def versionedProjectName = "${rootProject.name}-${project.version}"

task writeVersion() {
	doFirst {
		file('src/main/resources/version.properties').text = """app.name=${rootProject.name}
build.version=$version
build.date=${new java.text.SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())}
"""
	}
}

compileJava.dependsOn writeVersion

license {
	header = project.file('LICENSE.HEADER')

	ext.year = Calendar.getInstance().get(Calendar.YEAR)

	headerDefinitions {
		star_dash_block {
			firstLine = "/*-"
			endLine   = " */"
			beforeEachLine = " * "
			firstLineDetectionPattern = "/\\*-\$"
			lastLineDetectionPattern = ".*\\*/(\\s|\\t)*\$"
			allowBlankLines = false
			isMultiline = true
		}
	}

	mapping {
		java = 'star_dash_block'
	}

	skipExistingHeaders = true

	include '**/*.java'
}

task jpackageImageWithExtras(type: Zip) {
	dependsOn jpackageImage

	def imgDir = file("build/jpackage/$versionedProjectName")

	doFirst {
		copy {
			from projectDir
			include 'LICENSE'
			into imgDir
		}
	}

	from projectDir
	include 'src/main/java/**/*'
	destinationDirectory = imgDir
	archiveFileName = "${versionedProjectName}-src.zip"
}

runtime {
	jpackage {
		imageName = "${versionedProjectName}"
	}
}

jpackage.dependsOn jpackageImageWithExtras
